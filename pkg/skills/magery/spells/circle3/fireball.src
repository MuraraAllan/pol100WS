use vitals;
use uo;
use cfgfile;

include ":attributes:attributes";
include ":magery:spells";
include "include/client";
include ":magery:spellUtils";
include "include/sounds";
include ":damage:damage";

program cast_fireball(parms)
  var caster := parms[1];
  var info := parms[2];
  var cast_on := MS_Target(caster, info.targ, "Selecione um alvo.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);
  var prevtime := ReadGameClock() + 8;
  if (CheckLostFocus({ prevtime , caster }) == 1)
    return 0; 
  endif
  var magery := GetAttribute(caster, MAGERY) / 30;
  var eval := GetAttribute(caster, EVALUATING_INTELLIGENCE) / 20;
  var mod_amount := RandomDiceRoll("6d2+1")+RandomInt(magery)+RandomInt(eval)+eval;
  if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
      cast_on := parms[4];
  endif
  if (!cast_on)
      SendSysMessage (caster, "Cancelado.", color := 33);
      return 0;
  endif
  if ( !MS_MobileCheck(caster, cast_on) )
      return 0;
  endif
var manaCost := parms[5];
  if (!useMana(caster, manaCost)) 
    SendSysMessage(caster, "You don't have enough mana to cast that spell!");
    return 0;
  endif
 
  SetObjProperty(cast_on, "LastHit", {caster.name,caster.serial, "fireball" });
  PlaySoundEffect(cast_on, 0x15f);
  PlayMovingEffect( caster, cast_on, FX_FIREBALL, 0x0a,0x1e);
  cast_on := reflectSpell({ caster, cast_on, 40, FX_FIREBALL, 0x15f, 800 });

  if(caster == cast_on)
    ApplyRawDamage(cast_on, mod_amount);
    PrintTextAbove(cast_on, " " + mod_amount + " ", FONT_SMALL_LIGHT, 38);
  else
    SetObjProperty(cast_on, "Lastataque", Readgameclock() + 5);
    var boolRes := ResistSpell({ caster, cast_on, 15 });
    if (boolRes == 1)
      SetObjProperty(cast_on, "resist", 1);
      SendSysMessage(cast_on, "Voce sente seu corpo resistindo a magia!", 9,89);
      PlaySoundEffect(cast_on, SFX_SPELL_WEAKEN);
      PlayObjectCenteredEffect(cast_on, FX_CURSE_EFFECT, 5,5);
      ApplyRawDamage(cast_on, mod_amount/2);
      PrintTextAbove(cast_on, " " + mod_amount/2 + " ", FONT_SMALL_LIGHT, 38);
    else
      ApplyRawDamage(cast_on, mod_amount);
      PrintTextAbove(cast_on, " " + mod_amount + " ", FONT_SMALL_LIGHT, 38);
    endif
  endif
  return 1;
           
endprogram
